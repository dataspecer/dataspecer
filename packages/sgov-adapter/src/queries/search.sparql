DEFINE sql:signal-void-variables 0

PREFIX z: <https://slovník.gov.cz/základní/pojem/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    <__search_results> <__has_search_result> ?node .
	?node
		<__order> ?len ;
		skos:prefLabel ?label ;
		skos:definition ?definition ;
		skos:inScheme ?glossary ;
		<__is_ciselnik> ?is_ciselnik .
} WHERE {
	# Class is skos:Concept but not z:typ-vlastnosti or z:typ-vztahu
	# This will includes also meta-classes from https://slovník.gov.cz/základní/glosářglosář
	?node a skos:Concept .
	MINUS { ?node a z:typ-vlastnosti . }
	MINUS { ?node a z:typ-vztahu . }

	?node	skos:prefLabel ?label .

	OPTIONAL { ?node skos:definition ?definition . }
	OPTIONAL { ?node skos:scopeNote ?definition . }
	OPTIONAL { ?node skos:inScheme ?glossary . }
	BIND(EXISTS {?node rdfs:subClassOf+ <https://slovník.gov.cz/datový/číselníky/pojem/položka-číselníku>} as ?is_ciselnik)

	{
		select ?node (MIN(strlen(str(?search_label)))  as ?len) WHERE {
			?node skos:prefLabel ?search_label .
			FILTER (regex(lcase(str(?search_label)), %QUERY%, "i"))
		}
		GROUP BY ?node
		ORDER BY ?len
		LIMIT 30
	}
}
