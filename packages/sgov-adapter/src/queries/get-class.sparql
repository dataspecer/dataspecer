DEFINE sql:signal-void-variables 0

PREFIX z: <https://slovník.gov.cz/základní/pojem/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
  ?node a z:typ-objektu ;
    rdfs:subClassOf ?ancestor ;
    skos:prefLabel ?label ;
    skos:definition ?definition;
    skos:inScheme ?glossary ;
    <__is_ciselnik> ?is_ciselnik .
} WHERE {
	# Class is skos:Concept but not z:typ-vlastnosti or z:typ-vztahu
 	# This will includes also meta-classes from https://slovník.gov.cz/základní/glosářglosář
 	?node a skos:Concept .
 	MINUS { ?node a z:typ-vlastnosti . }
 	MINUS { ?node a z:typ-vztahu . }
 
  OPTIONAL { ?node skos:prefLabel ?label . }
  OPTIONAL {
    ?node rdfs:subClassOf ?ancestor .
    ?ancestor a z:typ-objektu .
#    FILTER NOT EXISTS {
#        ?ancestor skos:inScheme <https://slovník.gov.cz/základní/glosář> .
#    }

    OPTIONAL {
        ?ancestor rdfs:subClassOf ?elementParent .
        ?elementParent skos:inScheme [] .
        FILTER NOT EXISTS {
            ?elementParent skos:inScheme <https://slovník.gov.cz/základní/glosář> .
        }
    }
  }
  OPTIONAL { ?node skos:definition ?definition . }
  OPTIONAL { ?node skos:scopeNote ?definition . }
  OPTIONAL { ?node skos:inScheme ?glossary . }
  BIND(EXISTS {?node rdfs:subClassOf+ <https://slovník.gov.cz/datový/číselníky/pojem/položka-číselníku>} as ?is_ciselnik)
  FILTER (?node = %NODE%)
}
